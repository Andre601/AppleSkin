buildscript {
	repositories {
		maven { url = 'https://files.minecraftforge.net/maven' }
		jcenter()
		mavenCentral()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
	}
}
apply plugin: 'net.minecraftforge.gradle'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

sourceSets {
	api
	main {
		compileClasspath += sourceSets.api.output
		runtimeClasspath += sourceSets.api.output
	}
	test
}

configurations {
	apiCompile.extendsFrom(compile)
}

minecraft {
	mappings channel: mappings_channel, version: mappings_version

	// default run configurations.
	// these can be tweaked, removed, or duplicated as needed.
	runs {
		client {
			workingDirectory project.file('run')
			mods {
				appleskin {
					source sourceSets.main
					source sourceSets.api
				}
			}
		}

		server {
			workingDirectory project.file('run')
			mods {
				appleskin {
					source sourceSets.main
					source sourceSets.api
				}
			}
		}
	}
}

group = project.projectDir.name.toLowerCase()
def modId = project.projectDir.name
archivesBaseName = modId + "-mc" + minecraft_version

dependencies {
	minecraft 'net.minecraftforge:forge:'+minecraft_version+'-'+forge_version
}

processResources {
	inputs.property "vars", project.version
	from(sourceSets.main.resources.srcDirs) {
		include '**/mods.toml'
		expand 'version':project.version
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude '**/mods.toml'
	}
}

jar {

	from sourceSets.main.output.classesDirs
	from sourceSets.api.output.classesDirs
	from sourceSets.main.output.resourcesDir
	from sourceSets.api.output.resourcesDir

	manifest {
		attributes([
			"Specification-Title": "appleskin",
			"Specification-Vendor": "squeek",
			"Specification-Version": "1",
			"Implementation-Title": project.name,
			"Implementation-Version": "${version}",
			"Implementation-Vendor" :"squeek",
			"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

task sourcesJar(type: Jar) {
	classifier = "sources"
	from sourceSets.main.allJava
	from sourceSets.api.allJava
}

task apiJar(type: Jar) {
	classifier = "api"
	from sourceSets.api.output
	include "squeek/appleskin/api/**"
}

artifacts {
	archives sourcesJar
	archives apiJar
}

